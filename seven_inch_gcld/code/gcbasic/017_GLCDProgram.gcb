#IGNORECOMPILATION

/*

Super Computer display

Using the drawing of a horizontal table window at (20, 20) position. The sub-table size is 80*40, table
number of row is 9, table number of column is 11, the frame color is red, foreground color of
Item table is green, foreground color of content table is white, inner frame width is 1, outer
frame width is 3.
GLCDMakeTable (XSTARTPOS, YSTARTPOS, CELLW, CELLH, NUMOFCOLS, NUMOFROWS, 1, 3, 3, TFT_RED, TFT_GREEN, TFT_WHITE)
*/
    #define XSTARTPOS           3
    #define YSTARTPOS           0
    #define CELLW               15
    #define CELLH               15
    #define NUMOFCOLS           ( GLCDDeviceWidth- XSTARTPOS  ) / CELLW  'calc will fill screen
    #define NUMOFROWS           ( GLCDDeviceHeight- YSTARTPOS ) / CELLH  'calc will fill screen
    #define INNERFRAME          1
    #define OUTERFRAME          3
    // These control the accent of the table
    #define TOPROWACCENT        2
    #define LEFTCOLUMNACCENT    1
    #define NOACCENT            0
    

    // Set PWM for LCD
    LT7686_PWM_DUTY ( LT7686_PWM_PRESCALER_MAX )

    // Dim the variables
    Dim logadc, posx, posy, printx, printy, randomx, randomy, randomcolor as word

    // Make the TABLE
    GLCDMakeTable ( XSTARTPOS, YSTARTPOS, CELLW, CELLH, NUMOFCOLS, NUMOFROWS, INNERFRAME, OUTERFRAME,  TOPROWACCENT+LEFTCOLUMNACCENT, TFT_RED, TFT_BLUE, TFT_WHITE)
    
    // Set the BackgroundColor
    GLCDSetBackgroundColor ( TFT_WHITE )
    LT7686_PWM_DUTY ( 100 )

    wait 2 s

    Randomize READAD( AN0 )
    
    Do

        //  Generate some random numbers to select color, and which table cell
        randomcolor = Random
        
        // Multiply by 256 to get more colors - this is faster!
        Set C Off
        Repeat 8 
            rotate randomcolor left
        End Repeat

        randomx = Scale( Random, 0, 255, 1, NUMOFCOLS -1 ) 
        randomy = Scale( Random, 0, 255, 1, NUMOFROWS -1 )

        // Convert cell to XY position
        posx = randomx * CELLW
        posy = randomy * CELLH
                
        // Calculate the XY position on the GLCD
        printx = posx + XSTARTPOS + 3
        printy = posy + YSTARTPOS + 3 

        if randomcolor = 0 then 
            randomcolor = TFT_WHITE
        end if

        FilledBox ( posx + XSTARTPOS + 2, posy + YSTARTPOS + 2, posx + XSTARTPOS + CELLW - 2 , posy + YSTARTPOS + CELLH - 2 , randomcolor )
        // FramedFilledbox ( posx + XSTARTPOS + 2, posy + YSTARTPOS + 2, posx + XSTARTPOS + CELLW - 2 , posy + YSTARTPOS + CELLH - 2 , 2, TFT_BLACK, randomcolor )

    Loop
